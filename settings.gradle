rootProject.name = 'spring-secutiry-guide'
[
    "example:spring-security-basic",
    "example:spring-security-form-login",
    "example:spring-security-custom-filter"
].each {
    if (it.indexOf("global") != -1) {
        def dir = makeDir(rootDir, it)
        intiProjectFolder(dir, it);
    } else {
        def projectNameArr = it.split(":")

        def prefixName = projectNameArr[0]
        def projectName = projectNameArr[1]
        def dir = makeDir(rootDir, prefixName)
        def subDir = makeDir(dir, projectName)
        intiProjectFolder(subDir, it);
    }

}

def intiProjectFolder(dir, projectFullName) {
    // add .gitigrore file
    def gitFile = new File(dir.absolutePath, ".gitignore")
    if (!gitFile.exists()) {
        gitFile.text = "build/"
    }

    def gradleFile = new File(dir.absolutePath, "build.gradle")
    if (!gradleFile.exists()) {
        gradleFile.text =
                """

                    dependencies {

                    }

                    """.stripIndent(20)
    }
    [
            "src/main/java/io/jjong",
            "src/main/resources",
            "src/test/java//io/jjong",
            "src/test/resources"
    ].each { srcDir ->
        def srcFolder = new File(dir.absolutePath, srcDir)
        if (!srcFolder.exists()) {
            srcFolder.mkdirs()
        }
    }
    def projectName = ":${projectFullName}"

    include projectName
    project(projectName).projectDir = dir
}


def makeDir(attachedDir, name) {
    def dir = new File(attachedDir, name)
    if (!dir.exists()) {
        dir.mkdirs()
    }
    return dir;
}